Filtron {
  Query = OrExpression

  OrExpression = AndExpression (or AndExpression)*

  AndExpression = NotExpression (and NotExpression)*

  NotExpression = not NotExpression -- negation
                | PrimaryExpression

  PrimaryExpression = "(" OrExpression ")" -- parens
                    | FieldExpression

  // Most common cases first to reduce backtracking
  FieldExpression = FieldName ComparisonOp Value -- comparison
                  | FieldName oneOfOp "[" NonemptyListOf<Value, ","> "]" -- oneOf
                  | FieldName notOneOfOp "[" NonemptyListOf<Value, ","> "]" -- notOneOf
                  | FieldName "?" -- existsQuestion
                  | FieldName exists -- existsKeyword
                  | FieldName -- booleanField

  FieldName = ident ("." ident)*

  oneOfOp = ":"
  notOneOfOp = "!:"
  
  // Two-char operators first, then single-char by frequency
  ComparisonOp = "!=" | ">=" | "<=" | "=" | ">" | "<" | "~" | ":"

  Value = stringLiteral
        | numberLiteral
        | booleanLiteral
        | ident ("." ident)* -- dottedIdent

  stringLiteral = "\"" stringChar* "\""
  stringChar = ~("\"" | "\\") any -- nonEscaped
             | "\\" any -- escaped

  numberLiteral = "-"? digit+ "." digit+ -- float
                | "-"? digit+ ~"." -- int

  booleanLiteral = true | false

  ident = ~keyword identStart identPart*
  identStart = letter | "_"
  identPart = alnum | "_"

  keyword = and | or | not | true | false | exists
  and = caseInsensitive<"and"> ~identPart
  or = caseInsensitive<"or"> ~identPart
  not = caseInsensitive<"not"> ~identPart
  true = caseInsensitive<"true"> ~identPart
  false = caseInsensitive<"false"> ~identPart
  exists = caseInsensitive<"exists"> ~identPart

  // Whitespace
  space += comment
  comment = "//" (~"\n" any)*
}
